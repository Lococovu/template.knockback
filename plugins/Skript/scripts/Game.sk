function PlayerEntry(player: player):
  # Настройки
  set {_player}'s gamemode to survival
  clear {_player}'s inventory
  
  equip({_player})

  # Проверяем игру
  checkGame()
  teleport {_player} to {game.%{game.currentId}%.spawn}

every 10 seconds:
  loop all players:
    if {authorized::%loop-player%} is true:
      add loop-player to {_players::*}

  reveal {_players::*} to {_players::*}

every 1 second:
  loop all players:
    if {authorized::%loop-player%} is true:
      if {game.current::king} is set:
        if "%loop-player%" is "%{game.current::king}%":
          glowing of loop-player is false
          set glowing of loop-player to true
        else:
          glowing of loop-player is true
          set glowing of loop-player to false

      {game.bossbar} is set
      skellett add loop-player to bossbar {game.bossbar}

  # Creating skellett bossbar
  if {game.bossbar} is not set:
    set {game.bossbar} to skellett new bossbar

    set skellett colour of bossbar {game.bossbar} to YELLOW
    set skellett style of bossbar {game.bossbar} to SEGMENTED_6
    skellett show bossbar {game.bossbar}

  # Progress
  set skellett progress of bossbar {game.bossbar} to 0

  # Title
  if {game.current::king} is not set:
    set skellett title of bossbar {game.bossbar} to "&e♜ &fУбейте больше всего игроков. Станьте королём игры!"
  else:
    set skellett title of bossbar {game.bossbar} to "&e♜ &fКороль: &e%{game.current::king}% &7(%{game.current::%{game.current::king}%.kills}% убийств)"

on damage:
  if distance between victim and {game.%{game.currentId}%.spawn} < 16:
    cancel event

  set {game.%victim%.lastHit} to attacker
  if damage cause is fall:
    cancel event
  if damage cause is void:
    kill victim
  else if "%damage cause%" is "projectile":
    set damage to 0

    set {_id} to random integer between 000 and 999
    set {temp.%attacker%.hit} to {_id}

    wait 4 seconds
    if {temp.%attacker%.hit} is {_id}:
      delete {temp.%attacker%.hit}
  else:
    set damage to 0

on script load:
  set {_time::1} to player head
  add "{SkullOwner:{Id:""cc580a79-3444-4a8a-a236-ffb3a4e584fc"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2E1MTZmYmFlMTYwNThmMjUxYWVmOWE2OGQzMDc4NTQ5ZjQ4ZjZkNWI2ODNmMTljZjVhMTc0NTIxN2Q3MmNjIn19fQ==""}]}}}" to item-nbt of {_time::1}

  set {_time::2} to player head
  add "{SkullOwner:{Id:""76e59870-f172-4a44-80b5-96f67058ce9e"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNDY5OGFkZDM5Y2Y5ZTRlYTkyZDQyZmFkZWZkZWMzYmU4YTdkYWZhMTFmYjM1OWRlNzUyZTlmNTRhZWNlZGM5YSJ9fX0=""}]}}}" to item-nbt of {_time::2}

  set {_time::3} to player head
  add "{SkullOwner:{Id:""756018d9-1746-423a-a851-438eeb9e0109"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZmQ5ZTRjZDVlMWI5ZjNjOGQ2Y2E1YTFiZjQ1ZDg2ZWRkMWQ1MWU1MzVkYmY4NTVmZTlkMmY1ZDRjZmZjZDIifX19""}]}}}" to item-nbt of {_time::3}

  set {_time::4} to player head
  add "{SkullOwner:{Id:""27b43599-aee5-4a1e-b5be-a43808aeaf5b"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZjJhM2Q1Mzg5ODE0MWM1OGQ1YWNiY2ZjODc0NjlhODdkNDhjNWMxZmM4MmZiNGU3MmY3MDE1YTM2NDgwNTgifX19""}]}}}" to item-nbt of {_time::4}

  set {_time::5} to player head
  add "{SkullOwner:{Id:""b03c6764-d56b-4453-a7a6-fed259ae58c9"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZDFmZTM2YzQxMDQyNDdjODdlYmZkMzU4YWU2Y2E3ODA5YjYxYWZmZDYyNDVmYTk4NDA2OTI3NWQxY2JhNzYzIn19fQ==""}]}}}" to item-nbt of {_time::5}

  set {_time::6} to player head
  add "{SkullOwner:{Id:""105548be-b9ba-4af2-afc8-ca319b8994e8"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvM2FiNGRhMjM1OGI3YjBlODk4MGQwM2JkYjY0Mzk5ZWZiNDQxODc2M2FhZjg5YWZiMDQzNDUzNTYzN2YwYTEifX19""}]}}}" to item-nbt of {_time::6}

  set {_time::7} to player head
  add "{SkullOwner:{Id:""5bcacad9-f80e-44bd-a8b4-f873166cf814"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMjk3NzEyYmEzMjQ5NmM5ZTgyYjIwY2M3ZDE2ZTE2OGIwMzViNmY4OWYzZGYwMTQzMjRlNGQ3YzM2NWRiM2ZiIn19fQ==""}]}}}" to item-nbt of {_time::7}

  set {_time::8} to player head
  add "{SkullOwner:{Id:""aaf47e31-6e22-4a86-82f3-3b03b759c855"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYWJjMGZkYTlmYTFkOTg0N2EzYjE0NjQ1NGFkNjczN2FkMWJlNDhiZGFhOTQzMjQ0MjZlY2EwOTE4NTEyZCJ9fX0=""}]}}}" to item-nbt of {_time::8}

  set {_time::9} to player head
  add "{SkullOwner:{Id:""801fe110-f64c-4bd5-bdb8-3a900778f9e2"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZDZhYmM2MWRjYWVmYmQ1MmQ5Njg5YzA2OTdjMjRjN2VjNGJjMWFmYjU2YjhiMzc1NWU2MTU0YjI0YTVkOGJhIn19fQ==""}]}}}" to item-nbt of {_time::9}

  set {_time::10} to player head
  add "{SkullOwner:{Id:""8f664d47-004b-4cb6-b28d-ef5f77c54227"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvN2FmM2ZkNDczYTY0OGI4NDdjY2RhMWQyMDc0NDc5YmI3NjcyNzcxZGM0MzUyMjM0NjhlZDlmZjdiNzZjYjMifX19""}]}}}" to item-nbt of {_time::10}

  set {getTimeIcon::1} to {_time::1}
  set {getTimeIcon::2} to {_time::2}
  set {getTimeIcon::3} to {_time::3}
  set {getTimeIcon::4} to {_time::4}
  set {getTimeIcon::5} to {_time::5}
  set {getTimeIcon::6} to {_time::6}
  set {getTimeIcon::7} to {_time::7}
  set {getTimeIcon::8} to {_time::8}
  set {getTimeIcon::9} to {_time::9}
  set {getTimeIcon::10} to {_time::10}

on drop:
  cancel event

on pickup:
  cancel event

on death:
  # Проверяем то, от кого умер игрока
  if {game.%player%.lastHit} is set:
    set {_player} to {game.%player%.lastHit}
    delete {game.%player%.lastHit}

    if "%{_player}%" isn't "%player%":
      broadcast "%{_player}% убил %player%"

      add 1 to {game.current::%{_player}%.kills}
      add 1 to {statistics::%{_player}%::kills}

      if {game.current::king} is not set:
        set {game.current::king} to {_player}
      else:
        if {game.current::%{_player}%.kills} > {game.current::%{game.current::king}%.kills}:
          set {game.current::king} to {_player} 

  force player to respawn
  set death message to ""

on respawn:
  teleport player to {game.%{game.currentId}%.spawn}
  equip(player)

#
# Abilities
# 

# bow
on shoot:
  if projectile is arrow:
    set {_item} to shooter's held item
    
    set {_template::empty} to "◻◻◻◻◻◻◻◻◻"
    set {_template::full} to "◼◼◼◼◼◼◼◼"

    set {_cooldown} to 1
    while {_cooldown} <= 4:
      set {_temp} to getTimeIcon(round(5 - {_cooldown}))
      set {_temp}'s name to "##"
      replace all "##" in {_temp}'s name with "&e%first {_cooldown} characters of {_template::full}%&7%first 5 - {_cooldown} characters of {_template::empty}%"

      if {temp.%shooter%.hit} is set:
        delete {temp.%shooter%.hit}
        send title "&e➹ &fЛук" with subtitle "&c⚔" to shooter for 2 seconds with 0 ticks fade in and 2 ticks fade out

        set slot 1 of shooter's inventory to {_item}
        set slot 9 of shooter's inventory to arrow    

        stop

      set slot 1 of shooter's inventory to {_temp} 

      add 0.05 to {_cooldown}
      wait 1 tick

    #
    send title "&e➹ &fЛук" with subtitle "&e◼◼◼◼" to shooter for 2 seconds with 0 ticks fade in and 2 ticks fade out

    set slot 1 of shooter's inventory to {_item}
    set slot 9 of shooter's inventory to arrow
  else if projectile is ender pearl:
    set {_item} to shooter's held item

    set {_template::empty} to "◻◻◻◻◻◻◻◻◻"
    set {_template::full} to "◼◼◼◼◼◼◼◼"

    set {_cooldown} to 1
    while {_cooldown} <= 5:
      set {_temp} to getTimeIcon(round(6 - {_cooldown}))
      set {_temp}'s name to "##"
      replace all "##" in {_temp}'s name with "&e%first {_cooldown} characters of {_template::full}%&7%first 6 - {_cooldown} characters of {_template::empty}%"

      set slot 3 of shooter's inventory to {_temp} 

      add 0.05 to {_cooldown}
      wait 1 tick

    #
    send title "&e○ &fПёрл" with subtitle "&e◼◼◼◼◼◼" to shooter for 2 seconds with 0 ticks fade in and 2 ticks fade out
    set slot 3 of shooter's inventory to {_item}

on pressure plate:
  cancel event	
  push the player upwards at speed 1.0
  push the player forwards at speed 0.8
  show mob spawner flames at event-block

on flight toggle:
	player's gamemode is not creative:
		cancel event
		wait a tick
		set player's flight state to false
		push player upwards at speed 0.8
		push player forwards at speed 0.8

on jump:
  block below player is not air or water:
    set player's flight state to true

on right click:
  if name of player's held item contains "◼" or "◻":
    cancel event
  else if name of player's held item contains "Мега-прыжок":
    set {_item} to player's held item

    set {_template::empty} to "◻◻◻◻◻◻◻◻◻◻◻"
    set {_template::full} to "◼◼◼◼◼◼◼◼◼◼"

    set {_cooldown} to 1
    while {_cooldown} < 10:
      set {_temp} to getTimeIcon(round(11 - {_cooldown}))
      set {_temp}'s name to "##"
      replace all "##" in {_temp}'s name with "&e%first {_cooldown} characters of {_template::full}%&7%first 11 - {_cooldown} characters of {_template::empty}%"

      set slot 4 of player's inventory to {_temp}

      add 0.05 to {_cooldown}
      wait 1 tick

    #
    send title "&e△ &fМега-прыжок" with subtitle "&e◼◼◼◼◼◼◼◼◼◼" to player for 2 seconds with 0 ticks fade in and 2 ticks fade out
    set slot 4 of player's inventory to {_item}


# speed

function getTimeIcon(time: number) :: item:
  return {getTimeIcon::%{_time}%}

on inventory click:
  cancel event

on offhand switch:
  cancel event

# other
on hunger bar change:
  cancel event
  set player's hunger to 20

# checkGame
function checkGame():
  if {game.currentId} is not set:
    randomizeMap()

# randomizeMap
every 1 minute:
  add 1 to {game.time}

  if {game.time}/4 is 1 or 2 or 3 or 4 or 5 or 6:
    loop all players:
      sendNotification(loop-player, "Новая карта через &e%20 - {game.time}% &fминут.", 6)

  if {game.time} >= 23:
    set {game.time} to 1
    randomizeMap()

function randomizeMap():
  delete {game.current::*}
  set {game.currentId} to random element of {games::*}

  loop all players:
    # notifications

    equip(loop-player)
    teleport loop-player to {game.%{game.currentId}%.spawn}

# 
# Game start
# 
function clear(p: player):	
	clear {_p}'s inventory
	clear {_p}'s level
	heal {_p}
	set {_p}'s hunger to 20

function equip(p: player):	
	clear({_p})
	set slot 0 of {_p} to unbreakable stick of sharpness 1 and knockback 2 named "&es"
	set slot 1 of {_p} to unbreakable bow named "&7︻デ═一"
	set slot 2 of {_p} to 64 sandstone
	set slot 3 of {_p} to ender pearl named "&es"
	set slot 4 of {_p} to gold pressure plate named "&es &fМега-прыжок"
	set slot 9 of {_p} to arrow


# 
# On place & brake
# 
on break:
  if {block::%location of event-block%::placed} is not set:
    cancel event
  else:
    delete {block::%location of event-block%::placed}

on place:
  if distance between player and {game.%{game.currentId}%.spawn} <= 16:
    cancel event
  else:
    if "%player's held item%" contains "sandstone":
      set {_id} to random integer between 000 and 999
      set {_location} to location of event-block

      set {block::%location of event-block%::placed} to {_id} 

      wait 10 seconds
      if {block::%{_location}%::placed} isn't {_id}:
        stop
        
      wait 1 second
      set block at {_location} to orange clay
      wait 1 second
      set block at {_location} to red clay

      wait 2 seconds
      set block at {_location} to air
    else:
      set {_id} to random integer between 000 and 999
      set {_location} to location of event-block

      set {block::%location of event-block%::placed} to {_id}

      wait 10 seconds
      if {block::%{_location}%::placed} isn't {_id}:
        stop
      
      set block at {_location} to air

command setSpawn [<string>]:
  trigger:
    set {game.%arg-1%.spawn} to player's location
    if {games::*} doesn't contain arg-1:
      add arg 1 to {games::*}